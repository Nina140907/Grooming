
&НаСервере
Процедура ЗаполнитьНаСервере() 
	Объект.Начисления.Очистить();
	ДатаНачалаПериода = НачалоМесяца(Объект.ПериодНачисления);
	ДатаОкончанияПериода = КонецМесяца(Объект.ПериодНачисления);
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
	               |	КадроваяИсторияСотрудников.Период КАК ДатаНачальная,
	               |	КадроваяИсторияСотрудников.Оклад КАК ПоказательРасчета,
	               |	КадроваяИсторияСотрудников.ГрафикРаботы КАК ГрафикРаботы,
	               |	КадроваяИсторияСотрудников.ДатаОкончания КАК ДатаКонец
	               |ИЗ
	               |	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
	               |ГДЕ
	               |	КадроваяИсторияСотрудников.Период <= &ДатаОкончанияПериода
	               |	И КадроваяИсторияСотрудников.ДатаОкончания > &ДатаНачалаПериода
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	КадроваяИсторияСотрудников.Сотрудник.Наименование";
	Запрос.УстановитьПараметр("ДатаОкончанияПериода",ДатаОкончанияПериода);  
	Запрос.УстановитьПараметр("ДатаНачалаПериода", ДатаНачалаПериода);
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		НоваяСтрока = Объект.Начисления.Добавить();
		Если Результат.ДатаНачальная < ДатаНачалаПериода Тогда
			НоваяСтрока.ДатаНачала = ДатаНачалаПериода;
		Иначе
			НоваяСтрока.ДатаНачала = Результат.ДатаНачальная;
		КонецЕсли;  
		Если Результат.ДатаКонец > ДатаОкончанияПериода Тогда
			НоваяСтрока.ДатаОкончания = ДатаОкончанияПериода;
		Иначе 
			НоваяСтрока.ДатаОкончания = Результат.ДатаКонец;
		КонецЕсли;
		НоваяСтрока.ВидРасчета = ПредопределенноеЗначение("ПланВидовРасчета.Начисления.Оклад");
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Результат);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры
