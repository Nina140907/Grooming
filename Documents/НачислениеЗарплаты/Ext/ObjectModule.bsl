
Процедура ОбработкаПроведения(Отказ, Режим)
	Движения.Начисления.Записывать = Истина; 
	Движения.УчетЗатрат.Записывать = Истина; 
	Движения.Хозрасчетный.Записывать = Истина;
	
	// регистр Начисления

	Для Каждого ТекСтрокаНачисления Из Начисления Цикл
		Движение = Движения.Начисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрокаНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = ТекСтрокаНачисления.ДатаОкончания;
		Движение.ПериодРегистрации = ПериодНачисления;
		Движение.БазовыйПериодНачало = ТекСтрокаНачисления.ДатаНачала;
		Движение.БазовыйПериодКонец = ТекСтрокаНачисления.ДатаНачала;
		Движение.Сотрудник = ТекСтрокаНачисления.Сотрудник;
		Движение.Сумма = ТекСтрокаНачисления.ПоказательРасчета;
		Движение.ПоказательРасчета = ТекСтрокаНачисления.ПоказательРасчета;
		Движение.График = ТекСтрокаНачисления.ГрафикРаботы;
	КонецЦикла;
    Движения.Начисления.Записать();  
	Движения.Начисления.РассчитатьСуммуНачисления(); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Начисления.Сотрудник КАК Сотрудник,
	               |	Начисления.Сумма КАК Сумма,
	               |	Начисления.ВидРасчета.СтатьяЗатрат КАК СтатьяЗатрат
	               |ИЗ
	               |	РегистрРасчета.Начисления КАК Начисления
	               |ГДЕ
	               |	Начисления.Регистратор = &Регистратор
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	СтатьяЗатрат";
	Запрос.УстановитьПараметр("Регистратор",Ссылка);  
	
	// Регистр УчетЗатрат

	ВыборкаЗатрат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 		
	Пока ВыборкаЗатрат.Следующий() Цикл
		ДвижениеЗатрат = Движения.УчетЗатрат.Добавить();
		ДвижениеЗатрат.Период = Дата;
		ЗаполнитьЗначенияСвойств(ДвижениеЗатрат, ВыборкаЗатрат); 
		
	// Регистр Хозрасчетный
	
		Выборка = ВыборкаЗатрат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДвижениеХоз = Движения.Хозрасчетный.Добавить();
			ДвижениеХоз.Период = Дата;
			ДвижениеХоз.СчетДт = ПланыСчетов.Хозрасчетный.РасходыНаПродажу;
			ДвижениеХоз.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
			ДвижениеХоз.Содержание = "Отражено начисление заработной платы сотрудникам";
			ДвижениеХоз.Сумма = Выборка.Сумма;
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХоз.СчетДт,ДвижениеХоз.СубконтоДт,Выборка.СтатьяЗатрат);
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХоз.СчетКт, ДвижениеХоз.СубконтоКт, Выборка.Сотрудник);
		КонецЦикла;
	КонецЦикла;
		
	
КонецПроцедуры
