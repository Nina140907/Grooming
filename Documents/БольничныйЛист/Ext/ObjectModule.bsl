
Процедура ОбработкаПроведения(Отказ, Режим)
	Движения.Начисления.Записывать = Истина; 
	Движения.УчетЗатрат.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	Запрос = Новый запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КадроваяИсторияСотрудниковСрезПоследних.Оклад КАК Оклад
	               |ИЗ
	               |	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(&Период, Сотрудник = &Сотрудник) КАК КадроваяИсторияСотрудниковСрезПоследних";
	Запрос.УстановитьПараметр("Период", ЭтотОбъект.ДатаОкончания);
	Запрос.УстановитьПараметр("Сотрудник", ЭтотОбъект.Сотрудник);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ПараметрРасчета = 0;
	Иначе
		ПараметрРасчета = Результат.Выгрузить()[0].Оклад;
	КонецЕсли;
	
	МесяцНачисления = НачалоМесяца(ЭтотОбъект.ДатаНачала);
	МесяцОкончания = НачалоМесяца(ЭтотОбъект.ДатаОкончания); 
	
	// Регистр Начисления
	
	Пока МесяцНачисления <= МесяцОкончания Цикл 	
		Движение = Движения.Начисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.Начисления.Больничный;
		Движение.ПериодДействияНачало = Макс(ЭтотОбъект.ДатаНачала, МесяцНачисления);
		Движение.ПериодДействияКонец = Мин(ЭтотОбъект.ДатаОкончания, КонецМесяца(МесяцНачисления));
		Движение.ПериодРегистрации = ПериодНачисления;
		Движение.БазовыйПериодНачало = Макс(ЭтотОбъект.ДатаНачала, МесяцНачисления);
		Движение.БазовыйПериодКонец = Мин(ЭтотОбъект.ДатаОкончания, КонецМесяца(МесяцНачисления));
		Движение.Сотрудник = ЭтотОбъект.Сотрудник; 
		Движение.ПоказательРасчета = ПараметрРасчета; 	
		МесяцНачисления = ДобавитьМесяц(МесяцНачисления,1);
	КонецЦикла;
	Движения.Начисления.Записать();
	Движения.Начисления.РассчитатьСуммуНачисления();

    Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Начисления.Сотрудник КАК Сотрудник,
	               |	Начисления.Сумма КАК Сумма,
	               |	Начисления.ВидРасчета.СтатьяЗатрат КАК СтатьяЗатрат
	               |ИЗ
	               |	РегистрРасчета.Начисления КАК Начисления
	               |ГДЕ
	               |	Начисления.Регистратор = &Регистратор
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	СтатьяЗатрат";
	Запрос.УстановитьПараметр("Регистратор", Ссылка);  
	
	 // регистр Учет затрат   
	 
	ВыборкаЗатрат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗатрат.Следующий() Цикл
		ДвижениеЗатрат = Движения.УчетЗатрат.Добавить();
		ДвижениеЗатрат.Период = Дата;
		ЗаполнитьЗначенияСвойств(ДвижениеЗатрат, ВыборкаЗатрат); 
		
	// регистр Хозрасчетный
	
		Выборка = ВыборкаЗатрат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДвижениеХоз = Движения.Хозрасчетный.Добавить();
			ДвижениеХоз.Период = Дата;
			ДвижениеХоз.СчетДт = ПланыСчетов.Хозрасчетный.РасходыНаПродажу;
			ДвижениеХоз.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
			ДвижениеХоз.Содержание = "Отражено начисление по больничному листу";
			ДвижениеХоз.Сумма = Выборка.Сумма;
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХоз.СчетДт,ДвижениеХоз.СубконтоДт, Выборка.СтатьяЗатрат);
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХоз.СчетКт, ДвижениеХоз.СубконтоКт, Выборка.Сотрудник);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры
