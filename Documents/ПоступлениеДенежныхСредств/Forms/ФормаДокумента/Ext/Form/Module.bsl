&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	Плательщик = Объект.Плательщик;
	ВидОперации = Объект.ВидОперации;
	ТипДенежныхСредств = Объект.ТипДенежныхСредств;
	
	Если Объект.Ссылка.Пустая() ТОГДА
		Объект.ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Наличные;
		Объект.ВидОперации = Перечисления.ВидыОперацийПоступленияДенег.ОплатаОтПокупателя;
		Объект.Касса = Справочники.Кассы.ЦентральнаяКасса;
	КонецЕсли;
КонецПроцедуры    

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры  

&НаКлиенте
Процедура УстановитьВидимость() 
 	СтрокаТипПлательщика = "";	
 	Если Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные") Тогда
 		Элементы.ЭквайринговыйТерминал.Видимость = Ложь;  
		Элементы.Касса.Видимость = Истина; 
	ИначеЕсли Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные") Тогда
		Элементы.Касса.Видимость = Ложь; 
		Элементы.ЭквайринговыйТерминал.Видимость = Истина;
	КонецЕсли;

	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ВозвратОтПодотчетника") Тогда
		Элементы.ДоговорКонтрагента.Видимость = Ложь; 
		Элементы.ЭквайринговыйТерминал.Видимость = Ложь;
		Элементы.Касса.Видимость = Истина; 
		Элементы.РасчетныйСчет.Видимость = Ложь;
		Элементы.КассаИсточник.Видимость = Ложь;
		Элементы.Плательщик.Видимость = Истина;
		СтрокаТипПлательщика = "СправочникСсылка.Сотрудники";
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ПолучениеНаличныхВБанке")Тогда
		Элементы.ДоговорКонтрагента.Видимость = Ложь; 
		Элементы.Касса.Видимость = Истина; 
		Элементы.РасчетныйСчет.Видимость = Истина; 
		Элементы.КассаИсточник.Видимость = Ложь; 
		Элементы.Плательщик.Видимость = Истина;
		СтрокаТипПлательщика = "СправочникСсылка.Банки";
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ПоступлениеИзДругойКассы") Тогда
		Элементы.ДоговорКонтрагента.Видимость = Ложь;
		Элементы.ЭквайринговыйТерминал.Видимость = Ложь;
		Элементы.РасчетныйСчет.Видимость = Ложь; 
		Элементы.Плательщик.Видимость = Ложь;
		Элементы.Касса.Видимость = Истина;
		Элементы.КассаИсточник.Видимость = Истина; 
	Иначе		
		Элементы.ДоговорКонтрагента.Видимость = Истина; 
		Элементы.РасчетныйСчет.Видимость = Ложь;
		Элементы.КассаИсточник.Видимость = Ложь;   
		Элементы.Плательщик.Видимость = Истина;
		СтрокаТипПлательщика = "СправочникСсылка.Контрагенты"; 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТипПлательщика) Тогда         
        Массив = Новый Массив();
        Массив.Добавить(Тип(СтрокаТипПлательщика));    
        ОписаниеТипаПлательщика = Новый ОписаниеТипов(Массив);
        Элементы.Плательщик.ОграничениеТипа = ОписаниеТипаПлательщика;        
	КонецЕсли; 
	
	Если Элементы.ДоговорКонтрагента.Видимость = Истина
        И ЗначениеЗаполнено(Объект.Плательщик) Тогда
        Элементы.ДоговорКонтрагента.Видимость = ВидимостьДоговораКонтрагента.ПроверитьВидимостьДоговораВЗависимостиОтТипаКонтрагента(Объект.Плательщик);
    КонецЕсли; 	
   КонецПроцедуры


&НаКлиенте
Процедура ТипДенежныхСредствПриИзменении(Элемент)
	МассивОчищаемыхРеквизитов = Новый Массив;
	
	Если Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные")
	И ЗначениеЗаполнено(Объект.ЭквайринговыйТерминал) Тогда
		МассивОчищаемыхРеквизитов.Добавить("ЭквайринговыйТерминал");
		ТекстВопроса = "При изменении реквизита ""Тип оплаты"" на "+ Строка(Объект.ТипДенежныхСредств)+" будут очищены следующие данные:
		| - Эквайринговый терминал
		| Продолжить?";  
	ИначеЕсли Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные")
	И ЗначениеЗаполнено(Объект.Касса) Тогда
		МассивОчищаемыхРеквизитов.Добавить("Касса");
		ТекстВопроса = "При изменении реквизита ""Тип оплаты"" на "+ Строка(Объект.ТипДенежныхСредств)+" будут очищены следующие данные:
		| - Касса
		| Продолжить?"; 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов)Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииТипаденежныхСредств",ЭтаФорма,МассивОчищаемыхреквизитов);
		ПоказатьВопрос (Оповещение, ТекстВопроса,РежимДиалогаВопрос.ДаНет,,,"Внимание!");
	Иначе 
		ТипДенежныхСредств = Объект.ТипДенежныхСредств;
		УстановитьВидимость();  
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииТипаДенежныхСредств(Результат,ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
    	Для Каждого Реквизит Из ДополнительныеПараметры Цикл
        	Объект[Реквизит] = Неопределено;
    	КонецЦикла;
    	УстановитьВидимость();
    	ТипДенежныхСредств =  Объект.ТипДенежныхСредств;
	Иначе
   		Объект.ТипДенежныхСредств = ТипДенежныхСредств;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент) 
	МассивОчищаемыхРеквизитов = Новый Массив;
	
	Если (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ВозвратОтПодотчетника")
	ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ПолучениеНаличныхВБанке"))
	И ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		МассивОчищаемыхРеквизитов.Добавить("ДоговорКонтрагента");
		ТекстВопроса = "При изменении реквизита ""Вид операции"" на " + Строка(Объект.ВидОперации) + " будут очищены следующие данные:
		| - Договор контрагента
		| Продолжить?"; 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов)Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииВидаОпераций",ЭтаФорма,МассивОчищаемыхРеквизитов);
		ПоказатьВопрос (Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,"Внимание!"); 
	Иначе 
		ВидОперации = Объект.ВидОперации;
		УстановитьВидимость();  
	КонецЕсли;
КонецПроцедуры   
&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииВидаОпераций(Результат,ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
    	Для Каждого Реквизит Из ДополнительныеПараметры Цикл
        	Объект[Реквизит] = Неопределено;
    	КонецЦикла;
    	УстановитьВидимость();
    	ВидОперации =  Объект.ВидОперации;
	Иначе
   		Объект.ВидОперации = ВидОперации;
	КонецЕсли;
КонецПроцедуры    

&НаКлиенте
Процедура ПлательщикПриИзменении(Элемент) 
	МассивОчищаемыхРеквизитов = Новый Массив;
	
	ЭтоПоставщик = ВидимостьДоговораКонтрагента.ПроверитьВидимостьДоговораВЗависимостиОтТипаКонтрагента(Объект.Плательщик);
	МассивОчищаемыхРеквизитов = Новый Массив;
	Если Не ЭтоПоставщик И Значениезаполнено(Объект.ДоговорКонтрагента) Тогда
		МассивОчищаемыхРеквизитов.Добавить("ДоговорКонтрагента");
		ТекстВопроса = "При изменении реквизита ""Плательщик"" на " + Строка(Объект.Плательщик) + " будут очищены следующие данные:
		| - Договор контрагента
		| Продолжить?"; 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов)Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииПлательщика",ЭтаФорма,МассивОчищаемыхРеквизитов);
		ПоказатьВопрос (Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,"Внимание!"); 
	Иначе 
		Плательщик = Объект.Плательщик;
		УстановитьВидимость();  
	КонецЕсли;  
	УстановитьВидимость();
КонецПроцедуры  

&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииПлательщика(Результат,ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
    	Для Каждого Реквизит Из ДополнительныеПараметры Цикл
        	Объект[Реквизит] = Неопределено;
    	КонецЦикла;
    	УстановитьВидимость();
    	Плательщик =  Объект.Плательщик;
	Иначе
   		Объект.Плательщик = Плательщик;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьРеквизитыФормы();
	Если Параметры.Свойство("Основание")  
	И ЗначениеЗаполнено(Параметры.Основание)
	И ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.РеализацияТоваровИУслуг") Тогда
		СтруктураОплаты = Документы.РеализацияТоваровИУслуг.ПроверитьОплатуДокумента(Параметры.Основание);
		ПризнакОплаты = СтруктураОплаты.ПризнакОплаты;
		Если ПризнакОплаты = Перечисления.ПризнакиОплаты.ПолностьюОплачен Тогда
			СообщениеПользователю = Новый СообщениеПользователю();
			СообщениеПользователю.Текст = "Данный документ уже полностью оплачен.Ввод дополнительного документа оплаты не требуется!";
			СообщениеПользователю.Сообщить();
			Отказ = Истина;
			возврат;
		Иначе 
			Объект.СуммаДокумента = СтруктураОплаты.ОсталосьОплатить;
			ОсталосьОплатить = СтруктураОплаты.ОсталосьОплатить;
		КонецЕсли;
	КонецЕсли;   
	Если НЕ ЗначениеЗаполнено(Объект.Ответственный)Тогда
		Объект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли; 			
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	Если ОсталосьОплатить > 0 
	И Объект.СуммаДокумента > ОсталосьОплатить Тогда
		Объект.СуммаДокумента = ОсталосьОплатить;
	КонецЕсли;
КонецПроцедуры


